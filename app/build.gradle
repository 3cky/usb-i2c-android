apply plugin: 'com.android.application'
apply plugin: 'maven-publish'

android {
    namespace 'com.github.ykc3.android.usbi2c.app'

    defaultConfig {
        applicationId "com.github.ykc3.android.usbi2c.app"

        minSdkVersion versions.sdk.min
        targetSdkVersion versions.sdk.target
        compileSdk versions.sdk.compile

        versionCode versions.project.major * 1000000 + versions.project.minor * 1000 + versions.project.patch
        versionName versions.project.number
    }

    // Release management
    signingConfigs {
        release {
            File propsFile = project.rootProject.file('local.properties')
            if (propsFile.exists()) {
                Properties properties = new Properties()
                properties.load(propsFile.newDataInputStream())
                keyAlias properties.getProperty('key.alias')
                keyPassword properties.getProperty('key.password')
                storeFile file(properties.getOrDefault('key.store.file', 'release.keystore'))
                storePassword properties.getProperty('key.store.password')
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    publishing {
        singleVariant("release")
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId 'com.github.3cky'
                artifactId 'usb-i2c-android-app'
                version versions.project.number

                artifact source: file('build/outputs/apk/release/app-release.apk'),
                        classifier: 'signed', extension: 'apk'
            }
        }
    }
    publishReleasePublicationToMavenLocal.configure {
        mustRunAfter packageRelease
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.google.code.gson:gson:2.10'

    implementation project(':lib')
}
